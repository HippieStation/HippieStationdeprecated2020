/obj/item/multitool/var/datum/integrated_io/selected_io = null  //functional for integrated circuits.

//I know copypasting it and overwriting the old procs is a pretty rough thing to do, but hey, it's the simplest and most effective
/obj/item/multitool/examine(mob/user)
	..()
	if(selected_io)
		to_chat(user, "<span class='notice'>Activate [src] to detach the data wire.</span>")
	if(buffer)
		to_chat(user, "<span class='notice'>Its buffer contains [buffer].</span>")

/obj/item/multitool/attack_self(mob/user)
	if(selected_io)
		selected_io = null
		to_chat(user, "<span class='notice'>You clear the wired connection from the multitool.</span>")
	update_icon()

/obj/item/multitool/update_icon()
	if(selected_io)
		icon_state = "multitool_red"
	else
		icon_state = "multitool"

/obj/item/multitool/proc/wire(var/datum/integrated_io/io, mob/user)
	if(!io.holder.assembly)
		to_chat(user, "<span class='warning'>\The [io.holder] needs to be secured inside an assembly first.</span>")
		return

	if(selected_io)
		if(io == selected_io)
			to_chat(user, "<span class='warning'>Wiring \the [selected_io.holder]'s [selected_io.name] into itself is rather pointless.</span>")
			return
		if(io.io_type != selected_io.io_type)
			to_chat(user, "<span class='warning'>Those two types of channels are incompatible.  The first is a [selected_io.io_type], \
			while the second is a [io.io_type].</span>")
			return
		if(io.holder.assembly && io.holder.assembly != selected_io.holder.assembly)
			to_chat(user, "<span class='warning'>Both \the [io.holder] and \the [selected_io.holder] need to be inside the same assembly.</span>")
			return
		io.connect_pin(selected_io)

		to_chat(user, "<span class='notice'>You connect \the [selected_io.holder]'s [selected_io.name] to \the [io.holder]'s [io.name].</span>")
		selected_io.holder.interact(user) // This is to update the UI.
		selected_io = null

	else
		selected_io = io
		to_chat(user, "<span class='notice'>You link \the multitool to \the [selected_io.holder]'s [selected_io.name] data channel.</span>")

	update_icon()


/obj/item/multitool/proc/unwire(var/datum/integrated_io/io1, var/datum/integrated_io/io2, mob/user)
	if(!io1.linked.len || !io2.linked.len)
		to_chat(user, "<span class='warning'>There is nothing connected to the data channel.</span>")
		return

	if(!(io1 in io2.linked) || !(io2 in io1.linked) )
		to_chat(user, "<span class='warning'>These data pins aren't connected!</span>")
		return
	else
		io1.disconnect_pin(io2)
		to_chat(user, "<span class='notice'>You clip the data connection between the [io1.holder.displayed_name]'s \
		[io1.name] and the [io2.holder.displayed_name]'s [io2.name].</span>")
		io1.holder.interact(user) // This is to update the UI.
		update_icon()

/obj/item/multitool/ai_detect/update_icon()
	if(selected_io)
		icon_state = "multitool_red"
	else
		icon_state = "[initial(icon_state)][detect_state]"

/obj/item/multitool/syndicate
	name = "Black market multitool"
	desc = "This multitool is a favourite among saboteurs. Its optimised routine lets it reset circuitry faster but is otherwise a normal multitool."
	icon = 'hippiestation/icons/obj/tools.dmi'
	icon_state = "multitool_nuke"
	toolspeed = 0.5
